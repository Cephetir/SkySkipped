plugins {
    id "net.minecraftforge.gradle.forge" version "8f14deb"
    id "org.spongepowered.mixin" version "d5f9873d60"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "java"
}

version = "1.0"
group = "cephetir.simplemod"
archivesBaseName = "SimpleMod"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += "-Dfml.coreMods.load=cephetir.simplemod.tweaker.FMLLoadingPlugin"
    clientRunArgs += "--mixin mixins.simplemod.json"
}

repositories {
    mavenCentral()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
}

configurations {
    bundle
    implementation.extendsFrom(bundle)
}

dependencies {
    bundle("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
    }
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    provided files("libs/modcore.jar")
}

mixin {
    add sourceSets.main, "mixins.simplemod.refmap.json"
}

jar {

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "FMLCorePlugin": "cephetir.simplemod.tweaker.FMLLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.simplemod.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
    )
    enabled = false
}

shadowJar {
    archiveFileName = jar.archiveFileName
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.bundle]

    exclude "**/module-info.class"
    exclude "dummyThing"
    exclude "modcore.jar"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version":project.version, "mcversion":project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.reobfJar.dependsOn(shadowJar)